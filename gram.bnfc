Prog. Program  ::= [Stm] ;
Fun.  Function ::= "$" Ident "(" [PDecl] ")" ":" Type "do" [Stm] "end";

Dec.  Decl     ::= Ident ":" Type ;
PDec. PDecl	   ::= PMet ":" Type ;

terminator Function "" ;
terminator Stm "" ;
separator  Decl ";" ;
separator  PDecl ";" ;
separator  Exp "," ;

SNop.	 Stm ::= ";" ;
SDecl.   Stm ::= Decl ";"  ;
SExp.    Stm ::= Exp ";" ;
SBlock.  Stm ::= "do" [Stm] "end" ;
SWhile.	 Stm ::= "until" Exp "loop" [Stm] "end" ;
SWhileA. Stm ::= "from" Ass "until" Exp "loop" [Stm] "end" ;
SReturn. Stm ::= "return" Exp  ";" ;
SFunc.	 Stm ::= Function ;
DAss.    Ass ::= Exp12 ":=" Exp ";" ;
SAss.	 Stm ::= Ass ;
SCond.	 Stm ::= "if" Exp "then" [Stm] "end" ;
SCondEl. Stm ::= "if" Exp "then" [Stm] "else" [Stm] "end" ;
SWInt.	 Stm ::= "writeInt" "(" Exp ")" ";" ;
SWDou.	 Stm ::= "writeDouble" "(" Exp ")" ";" ;
SWStr.	 Stm ::= "writeString" "(" Exp ")" ";" ;

EOr.	 Exp2 ::= Exp2 "or" Exp3 ;
EAnd.	 Exp3 ::= Exp3 "and" Exp4 ;
EEql.	 Exp4 ::= Exp4 "==" Exp5 ;
ELt.     Exp5 ::= Exp5 "<" Exp6 ;
EGt.	 Exp5 ::= Exp5 ">" Exp6 ;
EAdd.    Exp6 ::= Exp6 "+" Exp7 ;
ESub.    Exp6 ::= Exp6 "-" Exp7 ;
EMul.    Exp7 ::= Exp7 "*" Exp8 ;
EDiv.	 Exp7 ::= Exp7 "/" Exp8 ;
ENot.	 Exp9 ::= "!" Exp8 ;
EDeref.	 Exp10 ::= "*" Exp10 ;
ERefer.	 Exp10 ::= "&" Exp10 ;
ERInt.	 Exp11 ::= "readInt" "(" ")" ;
ERDou.	 Exp11 ::= "readDouble" "(" ")" ;
ERStr.	 Exp11 ::= "readString" "(" ")" ;
EArr.	 Exp12 ::= Exp12 "[" Exp "]" ;
ECall.   Exp13 ::= Ident "(" [Exp] ")" ;
EVar.    Exp13 ::= Ident ;

EStr.    Exp13 ::= String ;
EInt.    Exp13 ::= Integer ;
EChar.	 Exp13 ::= Char ;
EDouble. Exp13 ::= Double ;
ETrue.	 Exp13 ::= "true" ;
EFalse.	 Exp13 ::= "false" ;

_.  Exp ::= Exp1 ;
_.  Exp1 ::= Exp2 ;
_.  Exp2 ::= Exp3 ;
_.  Exp3 ::= Exp4 ;
_.  Exp4 ::= Exp5 ;
_.  Exp5 ::= Exp6 ;
_.  Exp6 ::= Exp7 ;
_.  Exp7 ::= Exp8 ;
_.  Exp8 ::= Exp9 ;
_.  Exp9 ::= Exp10 ;
_.  Exp10 ::= Exp11 ;
_.  Exp11 ::= Exp12 ;
_.  Exp12 ::= Exp13 ;
_.  Exp13 ::= Exp14 ;
_.  Exp14 ::= "(" Exp ")";

TInt.    Type ::= "int" ;
TDouble. Type ::= "double" ;
TBool.	 Type ::= "bool" ;
TStr.	 Type ::= "string";
TChar.	 Type ::= "char";
TPtr.    Type ::= "*" Type;
TArray.	 Type ::= "array" "[" Type "]" ;
internal TMem.	Type ::= "&" Type ;

PVal.	 PMet ::= "value" Ident ;
PValres. PMet ::= "valres" Ident ;
PName.	 PMet ::= "name" Ident ;
PRef.    PMet ::= "ref" Ident ;
PImpl.	 PMet ::= Ident ;

comment	"--" ;
